	.title	DEMZX
	.enabl	LC, AMA

	.asect
	.=2120

; Reddie's 256-bytes demo 'bad tape'

Start:	mov	#1, R0
	emt	51				; set screen mode
	emt	53				; set background + foreground colors
	emt	62				; clear screen (background color #1)
	inc	R0
	inc	R0
	emt	53				; set colors

	; white rect
	mov	#4*8., R0
	mov	#30., R1
	emt	66				; set graph coords
	mov	#28.*8., R0
	mov	#256.-32., R1
	emt	76				; draw filled rectangle

	mov	#141000, R5			; ROM start addr for audio
	mov	#177663, R3	
	mov	#177716, R1

	; program
load:	mov	#400, R4
	call	PilotWait
	jsr	R2, PrintStr
	.word	3*400+4
	mov	(pc)+,r4
len:	.word	4000
	call	PilotWait

	asl	len
	asl	len
	bpl	load

	; error
	movb	#^B01000101, (R3)
	jsr	R2, PrintStr
	.word	21.*400+4

	br	.

PrintStr:
	mov	(R2)+, R0
	emt	57				; position
10$:	mov	#Msg001, R0
	emt	64				; printstr
	mov	R0, 10$+2
	rts	R2

PilotWait:
	movb	#^B01000100, (R3)
	clr	R0
10$:	cmp	(PC), (PC)
	sob	R0, 10$
	movb	#^B01001111, (R3)
20$:	cmp	(PC), (PC)
	sob	R0, 20$

Pilot:
	mov	#2000, R2
10$:	movb	#^B01000100, (R3)
	mov	#200, R0
	movb	R0, (R1)
	sob	R0, .
	movb	#^B01001111, (R3)
	movb	pc, (R1)
	mov	#170, R0
	sob	R0, .
	sob	R2, 10$

Data:
20$:	movb	(R5)+, R0
30$:	aslb	R0
	bcc	40$
	beq	60$
	movb	#^B01000100, (R3)
	movb	PC, (R1)
	br	50$
40$:	movb	#^B01001110, (R3)
	movb	#200, (R1)
50$:	mov	#60, R2
	sob	R2, .
60$:	sob	R4, 20$
	return

Msg001:	.asciz	"Program: tape error"
	.asciz	"Bytes: 256b intro  "
	.asciz	"R Tape loading error, 0:1"

	.end
