	.title	MENDE1

; demo for menestrel

LOA_DONT_CLRSCR =: 1
@include aloader.mac

Main:	; must modify exit by stop
	mov	#MainStop, Stop+2

	; init
	call	IniVis
	mov	#MsgHlo, R0
	call	Print

	; all patterns
	mov	#MusicStart, PatAddr
Loop:	; play one pattern
	clr	Chan0Wait
	clr	Chan1Wait
	clr	Chan2Wait
	mov	@PatAddr, R5
	bne	10$
	add	#2, PatAddr
	mov	@PatAddr, PatAddr
	br	Loop
10$:	tst	(R5)+
	mov	(R5)+, Chan1Addr
	mov	(R5)+, Chan2Addr
	mov	R5, Chan0Addr
	clr	PatEnd
	call	PlayChannels
	add	#2, PatAddr
	br	Loop

PlayChannels:
	mov	#Chan0Wait, R5
	call	PlayChannel
	mov	#Chan1Wait, R5
	call	PlayChannel
	mov	#Chan2Wait, R5
	call	PlayChannel
	; end of pattern?
	tst	PatEnd
	beq	10$
	return
10$:	; wait 1/50 sec
	mov	#177710, R0
	cmp	#400, (R0)
	bne	.-4
	; keypress?
	tst	KeyCur
	beq	PlayChannels
	jmp	MainStop

; R5 - #ChanXWait
PlayChannel:
	tst	PatEnd
	bne	99$
	dec	(R5)
	bpl	99$
	clr	(R5)				; wait ticks #
	mov	2(R5), R1			; note divisor
	mov	4(R5), R2			; chip/counter select bits
	mov	6(R5), R3			; notes data addr
	call	SetNot				; set current note
	mov	(R3), R4
	; is it end of pattern
	cmp	#255., R4
	bne	10$
	inc	PatEnd
	br	99$
10$:	; is it normal note
	bit	#^B11, R4
	bne	20$
	cmp	#4, R4				; special case - silence
	bne	12$
	mov	#2, 2(R5)
	br	90$
12$:	clc
	ror	R4
	mov	R4, 2(R5)			; set next tick note
	br	90$
20$:	; is it wait
	bit	#1, R4
	beq	30$
	asr	R4
	dec	R4
	mov	R4, (R5)
	br	90$
30$:	; its 1-tick wait and note
	inc	(R5)
	clc	
	ror	R4
	mov	R4, 2(R5)
90$:	add	#2, 6(R5)
99$:	return


; set inverted R0 to 177714
Out14:	com	R0
	bis	#^B0001000000000000, R0		; ~WR low
	mov	R0, @#177714
	bic	#^B0001000000000000, R0		; ~WR high
	mov	R0, @#177714
	com	R0
	return


; R1 - divisor, R2 - channel (upper byte)
SetNot:	mov	R2, R0
	clrb	R0
	bisb	R1, R0
	swab	R1
	call	Out14
	clrb	R0
	bisb	R1, R0
	swab	R1
	call	Out14
	return


; init VI53 counters
IniVis:	mov	#177714, R0
	mov	#^C^B0000001100110110, (R0)	; both chips, counter 0, mode 3 (~WR low)
	mov	#^C^B0001001100110110, (R0)	; ~WR high
	mov	#^C^B0000001101110110, (R0)
	mov	#^C^B0001001101110110, (R0)
	mov	#^C^B0000001110110110, (R0)
	mov	#^C^B0001001110110110, (R0)
	return


; print something with emt's
; R0 - msg addr
Print:	cmp	@#100000, #167
	beq	10$
	cmpb	@#177717, #300
	bne	10$
	emt	65
	return
10$:	mov	R1, -(SP)
	mov	R2, -(SP)
	mov	R0, R1
	clr	R2
	emt	20
	mov	(SP)+, R1
	mov	(SP)+, R2
	return


; stop counters (init VI's) and exit
MainStop:
	call	IniVis
	jmp	Exit


; variables
PatEnd:		.word	0
PatAddr:	.word	0

Chan0Wait:	.word	0
Chan0Note:	.word	2
Chan0Chip:	.word	0*256.+C_VI53_1
Chan0Addr:	.word	0

Chan1Wait:	.word	0
Chan1Note:	.word	2
Chan1Chip:	.word	1*256.+C_VI53_1
Chan1Addr:	.word	0

Chan2Wait:	.word	0
Chan2Note:	.word	2
Chan2Chip:	.word	2*256.+C_VI53_1
Chan2Addr:	.word	0

; messages
MsgHlo:	.ascii	<12><15>"-- Demo music for Menestrel --"<12><15>
	.ascii	"Conversion (by Kakos Nonos) of:"<12><15>
	.ascii	">baladinah_monstra_-tritone-.xm"<12><15>
	.asciz	">by raphaelgoulart"<12><15>
	.even

; music data
@include ./music/music3.mac


End:	.end	Start
